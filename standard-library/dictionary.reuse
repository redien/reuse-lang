
(typ (dictionary value) (Dictionary (list (pair (list int32) value))))

(def entry-get-key' (entry)
     (match entry
            (Pair key _) key))

(def entry-get-value' (entry)
     (match entry
            (Pair _ value) value))

(def entry-matches-key' (key entry)
     (string-equal? key (entry-get-key' entry)))

(def dictionary-empty () (Dictionary Empty))

(def dictionary-entries (dictionary)
     (match dictionary
            (Dictionary entries) entries))

(def dictionary-set (key value dictionary)
     (Dictionary (Cons (Pair key value)
                       (list-filter (pipe pair-left (string-equal? key) not) (dictionary-entries dictionary)))))

(def dictionary-of (entries)
     (list-foldl (pair-map dictionary-set) (dictionary-empty) entries))

(def dictionary-singleton (key value)
     (dictionary-set key value (Dictionary Empty)))

(def entries-matching-key' (key dictionary)
     ((pipe dictionary-entries
            (list-filter (entry-matches-key' key)))
        dictionary))

(def dictionary-get (key dictionary)
     ((pipe (entries-matching-key' key)
            list-first
            (maybe-map entry-get-value'))
        dictionary))

(def dictionary-get-or (key default dictionary)
     (match (dictionary-get key dictionary)
            (Some value)  value
            None          default))
