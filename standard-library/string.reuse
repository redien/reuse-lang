
(def string-of-char (character)
     (Cons character Empty))

(def string-first (string)
     (list-first string))

(def string-rest (string)
     (list-rest string))

(def string-concat (a b)
     (match a
            Empty       b
            (Cons x xs) (Cons x (string-concat xs b))))

(def string-append (a b)
     (string-concat b a))

(def string-join (separator list)
     (match list
            (Cons first rest)
                (list-foldl (fn (string joined)
                                (string-concat joined (string-concat separator string)))
                            first
                            rest)
            Empty
                Empty))

(def string-split' (separator string current parts)
     (match string
            Empty          (list-reverse (Cons (list-reverse current) parts))
            (Cons c rest)  (match (= separator c)
                                  True   (string-split' separator rest Empty (Cons (list-reverse current) parts))
                                  False  (string-split' separator rest (Cons c current) parts))))

(def string-split (separator string)
     (string-split' separator string Empty Empty))

(def string-empty? (string)
     (match string Empty True _ False))

(def string-equal? (a b)
     (match a
            (Cons xa xas) (match b
            (Cons xb xbs) (and (= xa xb) (string-equal? xas xbs))
            Empty         (string-empty? a))
            Empty         (string-empty? b)))



(def string-point-is-digit (point)
     (and (> point 47) (< point 58)))

(def string-to-int32'' (string-to-int32' string accumulator x)
     (string-to-int32' string (Some (+ (* 10 accumulator) (- x 48))))) 

(def string-to-int32' (string accumulator)
     (match string
            Empty
                accumulator
            (Cons x rest)
                (maybe-flatmap (fn (accumulator)
                    ((pipe
                            (maybe-filter string-point-is-digit)
                            (maybe-flatmap (string-to-int32'' string-to-int32' rest accumulator)))
                        (Some x)))
                    accumulator)))

(def string-to-int32 (string)
     (match string
            (Cons 45 string)
                (match (string-empty? string)
                       True   None
                       False  (maybe-map (fn (x) (* -1 x)) (string-to-int32 string)))
            _
                (string-to-int32' string (Some 0))))

(def string-from-int32' (integer string)
     (match (> integer 9)
            True
                (string-from-int32' (/ integer 10) (Cons (+ (% integer 10) 48) string))
            False
                (Cons (+ integer 48) string)))

(def string-from-int32 (integer)
     (match (< integer 0)
            True
                (match (= integer -2147483648)
                    True
                        (list 45 50 49 52 55 52 56 51 54 52 56)
                    False
                        (Cons 45 (string-from-int32 (* integer -1))))
            False
                (string-from-int32' integer Empty)))

