
(typ string-node (FTValue int32)
                 (FTNode2 string-node string-node)
                 (FTNode3 string-node string-node string-node))

(typ string FTEmpty
            (FTSingle string-node)
            (FTDeep (list string-node) string (list string-node)))

(def string-empty ()
     FTEmpty)

(def string-of-char (character)
     (FTSingle (FTValue character)))

(def string-cons' (a tree)
     (match tree
            FTEmpty
                (FTSingle a)
            (FTSingle x)
                (FTDeep (list a) FTEmpty (list x))
            (FTDeep first middle last)
                (match first
                       (Cons b (Cons c (Cons d (Cons e Empty))))
                            (FTDeep (list a b)
                                    (string-cons' (FTNode3 c d e) middle)
                                    last)
                       _
                            (FTDeep (Cons a first) middle last))))

(def string-cons (char string)
     (string-cons' (FTValue char) string))

(def string-first-node' (node)
     (match node
            (FTValue x)        x
            (FTNode2 x _)      (string-first-node' x)
            (FTNode3 x _ __)   (string-first-node' x)))

(def string-first (string)
     (match string
            (FTSingle node)     (Some (string-first-node' node))
            (FTDeep list _ __)  (maybe-map string-first-node' (list-first list))
            _                   None))

(def string-foldr-node' (f node identity)
     (match node
            (FTValue a)      (f a identity)
            (FTNode2 a b)    (string-foldr-node' f a (string-foldr-node' f b identity))
            (FTNode3 a b c)  (string-foldr-node' f a (string-foldr-node' f b (string-foldr-node' f c identity)))))

(def string-foldr (f identity tree)
     (match tree
            FTEmpty
                identity
            (FTSingle x)
                (string-foldr-node' f x identity)
            (FTDeep first middle last)
                (list-foldr (string-foldr-node' f)
                    (string-foldr f
                        (list-foldr (string-foldr-node' f) identity last)
                        middle)
                    first)))


(def string-to-list (string)
     (string-foldr list-cons Empty string))

(def string-from-list (list)
     (list-foldr string-cons (string-empty) list))

(def string-rest (string)
     (string-from-list (list-rest (string-to-list string))))

(def string-substring (start size string)
     (string-from-list (list-take size (list-skip start (string-to-list string)))))

(def string-concat (a b)
     (string-from-list (list-concat (string-to-list a) (string-to-list b))))

(def string-append (a b)
     (string-concat b a))

(def string-reverse (string)
     (string-from-list (list-reverse (string-to-list string))))

(def string-join' (separator list)
     (match list
            (Cons first rest)
                (list-foldl (fn (string joined)
                                (list-concat joined (list-concat separator string)))
                            first
                            rest)
            Empty
                Empty))

(def string-join (separator strings)
     (string-from-list (string-join' (string-to-list separator) (list-map string-to-list strings))))

(def string-flatmap (f string)
     (string-join (string-empty) (list-map f (string-to-list string))))

(def string-split' (separator list current parts)
     (match list
            Empty          (list-reverse (Cons (list-reverse current) parts))
            (Cons c rest)  (match (= separator c)
                                  True   (string-split' separator rest Empty (Cons (list-reverse current) parts))
                                  False  (string-split' separator rest (Cons c current) parts))))

(def string-split (separator string)
     (list-map string-from-list (string-split' separator (string-to-list string) Empty Empty)))

(def string-trim-start' (list)
     (match list
            (Cons x xs)
                (match (= x 32)
                       True   (string-trim-start' xs)
                       False  list)
            Empty
                list))

(def string-trim-start (string)
     (string-from-list (string-trim-start' (string-to-list string))))

(def string-trim-end (string)
     (string-reverse (string-trim-start (string-reverse string))))

(def string-trim (string)
     (string-trim-start (string-trim-end string)))

(def string-empty? (string)
     (match (string-first string)
            (Some _)  False
            None      True))

(def string-equal?' (a b)
     (match a
            (Cons xa xas) (match b
            (Cons xb xbs) (and (= xa xb) (string-equal?' xas xbs))
            Empty         (list-empty? a))
            Empty         (list-empty? b)))

(def string-equal? (a b)
     (string-equal?' (string-to-list a) (string-to-list b)))


(def string-point-is-digit (point)
     (and (> point 47) (< point 58)))

(def string-to-int32''' (string-to-int32'' string accumulator x)
     (string-to-int32'' string (Some (+ (* 10 accumulator) (- x 48))))) 

(def string-to-int32'' (string accumulator)
     (match string
            Empty
                accumulator
            (Cons x rest)
                (maybe-flatmap (fn (accumulator)
                    ((pipe
                            (maybe-filter string-point-is-digit)
                            (maybe-flatmap (string-to-int32''' string-to-int32'' rest accumulator)))
                        (Some x)))
                    accumulator)))

(def string-to-int32' (string)
     (match string
            (Cons 45 string)
                (match (list-empty? string)
                       True   None
                       False  (maybe-map (fn (x) (* -1 x)) (string-to-int32' string)))
            _
                (string-to-int32'' string (Some 0))))

(def string-to-int32 (string)
     (string-to-int32' (string-to-list string)))

(def string-from-int32'' (integer string)
     (match (> integer 9)
            True
                (string-from-int32'' (/ integer 10) (Cons (+ (% integer 10) 48) string))
            False
                (Cons (+ integer 48) string)))

(def string-from-int32' (integer)
     (match (< integer 0)
            True
                (match (= integer -2147483648)
                    True
                        (list 45 50 49 52 55 52 56 51 54 52 56)
                    False
                        (Cons 45 (string-from-int32' (* integer -1))))
            False
                (string-from-int32'' integer Empty)))

(def string-from-int32 (integer)
     (string-from-list (string-from-int32' integer)))

(def string-collect-from-indexed-iterator (predicate iterator)
     (pair-map-right string-from-list (list-collect-from-indexed-iterator predicate iterator)))
