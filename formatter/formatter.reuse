
(def stringify-error (error)
     (match error
            (MalformedDefinitionError            _)        (data-MalformedDefinitionError            )
            (MalformedTypeDefinitionError        _)        (data-MalformedTypeDefinitionError        )
            (MalformedFunctionDefinitionError    _ __)     (data-MalformedFunctionDefinitionError    )
            (MalformedFunctionNameError          _)        (data-MalformedFunctionNameError          )
            (MalformedExpressionError            _)        (data-MalformedExpressionError            )
            (MalformedMatchExpressionError       _)        (data-MalformedMatchExpressionError       )
            (MalformedSymbolError                _)        (data-MalformedSymbolError                )
            (MalformedConstructorError           _)        (data-MalformedConstructorError           )
            (MalformedTypeError                  _)        (data-MalformedTypeError                  )
            (ErrorNotDefined                     _ __ ___) (data-ErrorNotDefined                     )
            (ErrorAlreadyDefined                 _)        (data-ErrorAlreadyDefined                 )
            (ErrorReservedIdentifier             _ __ ___) (data-ErrorReservedIdentifier             )
             MalformedSexpTooFewClosingBrackets            (data-MalformedSexpTooFewClosingBrackets  )
             MalformedSexpTooManyClosingBrackets           (data-MalformedSexpTooManyClosingBrackets )))

(def space ()
     (string-of-char 32))

(def newline ()
     (string-of-char 10))

(def indent (n)
     (string-concat (newline) (string-repeat (space) n)))

(def list-with-space (items)
     (wrap-in-brackets (string-join (space) items)))

(def list-without-space (items)
     (wrap-in-brackets (string-join (string-empty) items)))

(def add-modifiers (public? rest)
     (match public?
          True
               (Cons (data-pub) rest)
          False
               rest))

(def format-pattern (pattern)
     (match pattern
            (IntegerPattern value _)
               (string-from-int32 value)
            (Capture identifier)
               (identifier-name identifier)
            (ConstructorPattern identifier patterns _)
               (list-with-space (Cons
                    (identifier-name identifier)
                    (list-map format-pattern patterns)))))

(def format-match-rule (format-expression depth rule)
     (match rule
            (Pair pattern expression)
               (string-join (string-empty) (list
                    (format-pattern pattern)
                    (indent (+ depth 3))
                    (format-expression (+ depth 3) expression)))))

(def format-match-rules (format-expression depth rules)
     (string-join (indent depth) (list-map (format-match-rule format-expression depth) rules)))

(def format-expression (depth expression)
     (match expression
            (IntegerConstant integer _)
                (string-from-int32 integer)
            (Variable identifier)
                (identifier-name identifier)
            (Lambda arguments expression _)
                (list-without-space (list
                    (data-fn)
                    (space)
                    (list-with-space (list-map identifier-name arguments))
                    (indent (+ depth 4))
                    (format-expression (+ depth 4) expression)))
            (Match expression rules range)
                (list-without-space (list
                    (data-match)
                    (space)
                    (format-expression depth expression)
                    (indent (+ depth 7))
                    (format-match-rules format-expression (+ depth 7) rules)))
            (Constructor identifier expressions range)
                (match expressions
                       Empty
                         (identifier-name identifier)
                       _
                         (list-with-space (Cons
                              (identifier-name identifier)
                              (list-map (format-expression depth) expressions))))
            (FunctionApplication expressions range)
                (list-with-space (list-map (format-expression depth) expressions))))

(def format-type (type)
     (match type
          (SimpleType identifier)
               (identifier-name identifier)
          (ComplexType identifier parameters _)
               (list-with-space (Cons (identifier-name identifier) (list-map format-type parameters)))
          (FunctionType argument-types return-type _)
               (list-with-space (list
                    (data-fn)
                    (list-with-space (list-map format-type argument-types))
                    (format-type return-type)))))

(def format-type-constructor (constructor)
     (match constructor
            (SimpleConstructor identifier)
               (identifier-name identifier)
            (ComplexConstructor identifier types _)
               (list-with-space (Cons (identifier-name identifier) (list-map format-type types)))))

(def format-type-parameter (parameter)
     (match parameter
          (UniversalParameter identifier)
               (identifier-name identifier)
          (ExistentialParameter identifier)
               (list-with-space (list (data-exists) (identifier-name identifier)))))

(def format-type-definition-name (name parameters)
     (match parameters
          Empty
               (identifier-name name)
          _
               (list-with-space (Cons (identifier-name name) (list-map format-type-parameter parameters)))))

(def format-type-definition-constructors (constructors)
     (match constructors
          (Cons constructor Empty)
               (string-concat (space) (format-type-constructor constructor))
          _
               (string-concat (indent 5) (string-join (indent 5) (list-map format-type-constructor constructors)))))

(def format-definition (definition)
     (match definition
            (TypeDefinition name public? parameters constructors _)
               (list-without-space (add-modifiers public? (list
                    (data-typ)
                    (space)
                    (format-type-definition-name name parameters)
                    (format-type-definition-constructors constructors))))
            (FunctionDefinition name public? arguments expression range)
               (list-without-space (add-modifiers public? (list
                    (data-def)
                    (space)
                    (identifier-name name)
                    (space)
                    (wrap-in-brackets (string-join (space) (list-map identifier-name arguments)))
                    (indent 5)
                    (format-expression 5 expression))))
            (TargetDefinition _ data)
                (string-from-slice data)))

(def format-file (definitions)
     (match (list-is-empty? definitions)
            True
               (string-empty)
            False
               ((pipe (list-map format-definition)
                      (string-join (string-repeat (newline) 2))
                      ((flip string-concat) (newline))
                      (string-concat (string-repeat (newline) 2)))
                     definitions)))

(def definition-file-path (definition)
     (source-reference-file-path (definition-source-reference definition)))

(def definitions-partition-by-file-name (definitions)
     (list-partition-by (fn (a b) (string-equal? (definition-file-path a) (definition-file-path b)))))

(def first-file-path (definitions)
     (maybe-map definition-file-path (list-first definitions)))

(def format-definitions (definitions)
     (match (first-file-path definitions)
            (Some file-path)
               (list (Pair file-path (string-to-slice (format-file definitions))))
            None
               Empty))

(pub def format-source-files (files)
     (result-bimap format-definitions
                   stringify-error
                   (parse-source-files (list-empty) files)))
