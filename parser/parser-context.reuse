
(typ (parser-context definition)
     (ParserContext (dictionary definition)))

(def parser-context-add-constructors (definition dictionary)
     (match definition
            (TypeDefinition _ __ ___ ____ constructors _____)
                (list-foldl (fn (constructor dictionary)
                                (dictionary-set (constructor-name constructor) definition dictionary))
                            dictionary
                            constructors)
            _
                dictionary))

(def parser-context-add (definition context)
     (match context
            (ParserContext constructors)
                (ParserContext (parser-context-add-constructors definition constructors))))

(def parser-context-is-name-constructor? (name context)
     (match context
            (ParserContext constructors)
                (match (dictionary-get name constructors)
                       (Some _)  True
                       None      False)))


(def parser-run (parser)
     (match (state-run (ParserContext (dictionary-empty)) parser)
            (Pair _ result)  result))

(def parser-return (value)
     (state-return (result-return value)))

(def parser-error (error)
     (state-return (result-error error)))

(def parser-bind (parser f)
     (state-bind parser (fn (result)
     (result-prod state-return
     (result-bind result (fn (value)
     (result-return (f value))))))))

(def parser-constructor? (name)
     (state-bind (state-get) (fn (context)
     (parser-return (parser-context-is-name-constructor? name context)))))

(def parser-add-definition! (definition)
     (state-bind (state-modify (parser-context-add definition)) (fn (_)
     (parser-return definition))))


(def parser-sequence (list)
     (list-foldr (fn (a b)
                     (parser-bind a (fn (a')
                     (parser-bind b (fn (b')
                     (parser-return (Cons a' b')))))))
                 (parser-return Empty)
                 list))
