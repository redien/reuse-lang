
(def identifier-sparkle-x () -1000)
(def identifier-cons () -1001)
(def identifier-empty () -1002)

(def with-local-transform-keywords (dictionary)
     (dictionary-set (data-sparkle-x) (Pair (identifier-sparkle-x) (data-sparkle-x))
     (dictionary-set (data-Cons) (Pair (identifier-cons) (data-Cons))
     (dictionary-set (data-Empty) (Pair (identifier-empty) (data-Empty))
     dictionary))))

(def expression-is-token? (token expression)
     (match expression
            (Variable (Identifier expression-token _ __ ___ ____))
                (= expression-token token)
            _
                False))

(def first-expression-is-token? (token expressions)
     ((pipe (maybe-map (expression-is-token? token))
            (maybe-else (fn () False)))
        (list-first expressions)))

(def transform-special-form (token transformer definition)
     (over-definition-expressions
     (over-subexpressions
     (over-function-application
         (fn (expressions range)
             (result-return (match (first-expression-is-token? token expressions)
                                   True   (transformer (definition-source-reference definition) (list-rest expressions) range)
                                   False  (FunctionApplication expressions range))))))
         definition))

(def transform-special-forms (token transformer definitions)
     (result-concat (list-map (transform-special-form token transformer) definitions)))

(def transform-pipe (source-reference expressions range)
     (Lambda (list (Identifier (identifier-sparkle-x) (data-sparkle-x) source-reference range None))
             (list-foldl (fn (expression composed)
                             (FunctionApplication (list expression composed) range))
                         (Variable (Identifier (identifier-sparkle-x) (data-sparkle-x) source-reference range None))
                         expressions)
             range))

(def transform-list (source-reference expressions range)
     (list-foldr (fn (expression composed)
                     (Constructor (Identifier (identifier-cons) (data-Cons) source-reference range None) (list expression composed) range))
                 (Constructor (Identifier (identifier-empty) (data-Empty) source-reference range None) Empty range)
                 expressions))

(def transform-match-expression (expression pairs range)
     (result-return (Match expression pairs range)))

(def transform-match-expressions (definition)
     (over-definition-expressions
     (over-subexpressions
     (over-match-expression
         (fn (expression pairs range)
             (transform-match-expression expression pairs range))))
         definition))

(def local-transforms (definitions)
     (result-bind definitions                                                         (fn (definitions)
     (result-bind (transform-special-forms (identifier-pipe) transform-pipe definitions)     (fn (definitions)
     (result-bind (transform-special-forms (identifier-list) transform-list definitions)     (fn (definitions)
     (result-bind (result-concat (list-map transform-match-expressions definitions))  (fn (definitions)
     (result-return definitions))))))))))
