
(typ type-inference-context (TypeInferenceContext (list (pair (list int32) type))
                                                  int32
                                                  (list (pair type type))))

(def type-inference-context-empty ()
     (TypeInferenceContext Empty 0 Empty))

(def type-inference-context-gensym-increment (n context)
     (match context
            (TypeInferenceContext types symbol-count constraints)
                (TypeInferenceContext types (+ symbol-count n) constraints)))

(def type-inference-context-symbol-from-count (count)
     (Cons 84 (string-from-int32 count)))

(def type-inference-context-symbol (context)
     (match context
            (TypeInferenceContext _ symbol-count __)
                (type-inference-context-symbol-from-count symbol-count)))

(def type-inference-context-symbols (n context)
     (match context
            (TypeInferenceContext _ symbol-count __)
                (list-map type-inference-context-symbol-from-count
                          (list-from-range symbol-count (+ symbol-count n)))))

(def gensym (f state)
     (state-modify (type-inference-context-gensym-increment 1)
     (state-flatmap-with-state (fn (context value)
                                   (f (type-inference-context-symbol context) value))
                               state)))

(def gensyms (count f state)
     (state-modify (type-inference-context-gensym-increment count)
     (state-flatmap-with-state (fn (context value)
                                   (f (type-inference-context-symbols count context) value))
                               state)))

(def type-inference-context-add-types (new-types context)
     (match context
            (TypeInferenceContext types symbol-count constraints)
                (TypeInferenceContext (list-concat new-types types) symbol-count constraints)))

(def type-inference-context-resolve (name context)
     (match context
            (TypeInferenceContext types _ __)
                (maybe-map pair-right
                           (list-find-first (fn (pair) (string-equal? name (pair-left pair))) types))))

(def type-inference-context-add-constraint (a b context)
     (match context
            (TypeInferenceContext types symbol-count constraints)
                (TypeInferenceContext types symbol-count (Cons (Pair a b) constraints))))

(def type-inference-context-constraints (context)
     (match context
            (TypeInferenceContext _ __ constraints)
                constraints))
