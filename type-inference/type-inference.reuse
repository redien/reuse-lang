
(def lookup-type-parameter (name parameters)
     (list-find-first (fn (parameter)
                          (match parameter
                                 (ConstType _ __)                  False
                                 (FnType _ __)                     False
                                 (UniversalType parameter-name)    (string-equal? name parameter-name)
                                 (ExistentialType parameter-name)  (string-equal? name parameter-name)))
                      parameters))

(def type-from-ast-type (parameters ast-type)
     (match ast-type
            (SimpleType name _)
                                                         (match (lookup-type-parameter name parameters)
                                                                (Some type)  type
                                                                None         (ConstType name Empty))
            (ComplexType name ast-types _)               (ConstType name (list-map (type-from-ast-type parameters) ast-types))
            (FunctionType argument-types return-type _)  (FnType (list-map (type-from-ast-type parameters) argument-types)
                                                                 (type-from-ast-type parameters return-type))))

(def type-from-constructor (return-type parameters constructor)
     (match constructor
            (SimpleConstructor name _)             (Pair name (FnType Empty return-type))
            (ComplexConstructor name ast-types _)  (Pair name (FnType (list-map (type-from-ast-type parameters) ast-types) return-type))))

(def type-from-parameter (parameter)
     (match parameter
            (UniversalParameter name _)    (UniversalType name)
            (ExistentialParameter name _)  (ExistentialType name)))

(def types-from-constructors (name parameters constructors)
     (list-map (type-from-constructor (ConstType name parameters) parameters) constructors))

(def infer-definition-type (definition)
     (gensym (fn (symbol definition)
                 (match definition
                        (TypeDefinition name parameters constructors meta)
                            (state-modify (fn (context)
                                              (type-inference-context-add-types (types-from-constructors name (list-map type-from-parameter parameters) constructors) context))
                                          (state-lift (TypeDefinition name parameters constructors (TypeAnnotation (UniversalType symbol) meta))))
                        (FunctionDefinition a b c meta)
                            (state-lift (FunctionDefinition a b c (TypeAnnotation (UniversalType symbol) meta)))
                        (ExportDefinition a b c meta)
                            (state-lift (ExportDefinition a b c (TypeAnnotation (UniversalType symbol) meta)))))
             definition))

(def infer-types (definitions)
     (state-final-value (type-inference-context-empty)
                        (state-all (list-map (pipe state-lift infer-definition-type) definitions))))
