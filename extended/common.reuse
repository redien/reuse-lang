
(def join (list) (string-join Empty list))

(def not-empty? (string)
     (not (string-empty? string)))

(def is-alphanumeric (char)
     (or (and (>= char 65) (<= char 90))
     (or (and (>= char 48) (<= char 57))
         (and (>= char 97) (<= char 122)))))

(def escape-char (char)
     (match (is-alphanumeric char)
            True   (list char)
            False  (Cons 95 (string-from-int32 char))))

(def stringify-parse-errors (definitions)
     (list-map (result-map-error error-to-string) definitions))

(def over-match-pair-expression (f pair)
     (match pair
            (Pair pattern expression)
                (result-map (fn (expression)
                                (Pair pattern expression))
                            (f expression))))

(def over-match-pair-expressions (over-subexpressions f pairs)
     (result-concat (list-map (over-match-pair-expression (pipe f (result-flatmap (over-subexpressions f)))) pairs)))

(def over-subexpressions (f expression)
     (match expression
            (Lambda arguments expression range)
                (result-flatmap (fn (expression)
                (result-map     (fn (expression)
                                    (Lambda arguments expression range))
                                (over-subexpressions f expression)))
                                (f expression))
            (Match expression pairs range)
                (result-flatmap (fn (expression)
                (result-flatmap (fn (expression)
                (result-map     (fn (pairs)
                                    (Match expression pairs range))
                                (over-match-pair-expressions over-subexpressions f pairs)))
                                (over-subexpressions f expression)))
                                (f expression))
            (Constructor name expressions range)
                (result-map (fn (expressions)
                                (Constructor name expressions range))
                            (result-concat (list-map (pipe f (result-flatmap (over-subexpressions f))) expressions)))
            (FunctionApplication expressions range)
                (result-map (fn (expressions)
                                (FunctionApplication expressions range))
                            (result-concat (list-map (pipe f (result-flatmap (over-subexpressions f))) expressions)))
            _
                (result-lift expression)))

(def over-definition-expressions (f definition)
     (match definition
            (FunctionDefinition name arguments expression range)
                (result-map (fn (expression)
                                (FunctionDefinition name arguments expression range))
                            (f expression))
            (ExportDefinition name arguments expression range)
                (result-map (fn (expression)
                                (ExportDefinition name arguments expression range))
                            (f expression))
            _
                (result-lift definition)))

(def over-function-application (f expression)
     (match expression
            (FunctionApplication expressions range)
                (f expressions range)
            _
                (result-lift expression)))

(def over-identifiers (f expression)
     (match expression
            (Identifier name range)
                (result-map (fn (name)
                                (Identifier name range))
                            (f name))
            (Lambda arguments expression range)
                (result-flatmap (fn (expression)
                (result-map     (fn (arguments)
                                    (Lambda arguments expression range))
                              (result-concat (list-map f arguments))))
                              (over-identifiers f expression))
            (Constructor name Empty range)
                (result-map (fn (name)
                                (Constructor name Empty range))
                            (f name))
            (Constructor name expressions range)
                (result-flatmap (fn (expressions)
                (result-map     (fn (name)
                                    (Constructor name expressions range))
                                (f name)))
                                (result-concat (list-map (over-identifiers f) expressions)))
            (FunctionApplication expressions range)
                (result-map (fn (expressions)
                                (FunctionApplication expressions range))
                            (result-concat (list-map (over-identifiers f) expressions)))
            (Match expression rules range)
                (result-flatmap (fn (rules)
                (result-map     (fn (expression)
                                    (Match expression rules range))
                              (over-identifiers f expression)))
                              (result-concat (list-map (over-match-pair-expression (over-identifiers f)) rules)))
            _
                (result-lift expression)))

(def expression-is-symbol? (symbol-name expression)
     (match expression
            (Identifier name __)
                (string-equal? symbol-name name)
            _
                False))

(def first-expression-is-symbol? (symbol-name expressions)
     ((pipe (maybe-map (expression-is-symbol? symbol-name))
            (maybe-else (fn () False)))
        (list-first expressions)))

(def symbol-is-reserved? (name)
     (match name
            (Cons 226 (Cons 156 (Cons 168 _)))                 True
            _                                                  False))

(def validate-identifier (identifier)
     (match (symbol-is-reserved? identifier)
            True   (result-error (data-error-reserved-identifier))
            False  (result-lift identifier)))

(def validate-reserved-identifiers (definition)
     (result-flatmap (over-definition-expressions (over-identifiers validate-identifier)) definition))
