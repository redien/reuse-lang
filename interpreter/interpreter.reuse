
(def return (x) (Result x))
(def bind (xs f) (result-bind xs f))
(def let (x f) (f x))
(def flatmap (f xs) (result-flatmap f xs))
(def sequence (xs) (result-concat xs))
(def map (f xs) (result-map f xs))

(def sym-main () -2005)
(def sym-Empty () -2006)
(def sym-Cons () -2007)

(def eval-error ()
     (Error (data-error-generic)))

(def pattern-matches-value (value pattern)
     (match (Pair value pattern)
            (Pair (ValueInteger a) (IntegerPattern b _2))
                (= a b)
            (Pair (ValueConstructor token name values) (ConstructorPattern pattern-symbol patterns _2))
                (and (= token (symbol-token pattern-symbol))
                     (list-every? (pair-map pattern-matches-value) (list-zip values patterns)))
            (Pair _1 (Capture _2))
                True
            _
                False))

(def case-capture (pair)
     (match pair
            (Pair value (IntegerPattern _1 _2))
                Empty
            (Pair (ValueConstructor _1 _2 values) (ConstructorPattern _3 patterns _4))
                (list-flatmap case-capture (list-zip values patterns))
            (Pair value (Capture sym))
                (list (Pair (symbol-token sym) value))
            _
                Empty))

(def render-stack-trace (stack-trace)
     (string-join (string-of-char 10)
                  (list-map (pipe (string-concat (data-space))
                                  (string-concat (data-space))
                                  (string-concat (data-space))
                                  (string-concat (data-space)))
                            stack-trace)))

(def add-stack-trace (expression values stack-trace)
     (maybe-or-else stack-trace
     (maybe-map (fn (expression)
                    (Cons (string-concat (string-append 32 (stringify-sexps (list-from (expression-to-sexp expression))))
                                         (string-join (data-space) (list-map value-to-string values)))
                          stack-trace))
                expression)))

(def pattern-match (eval-expression scope stack-trace value cases)
     (match (list-find-first (fn (case) (pattern-matches-value value (pair-left case))) cases)
            (Some (Pair pattern branch))
                (eval-expression (scope-add (case-capture (Pair value pattern)) scope) stack-trace branch)
            None
                (Error (string-join (string-empty)
                                    (list (data-error-unmatched-case)
                                          (string-of-char 10)
                                          (data-space) (data-space) (data-space) (data-space)
                                          (value-to-string value)
                                          (string-of-char 10)
                                          (data-error-unmatched-case2)
                                          (string-of-char 10)
                                          (data-space) (data-space) (data-space) (data-space)
                                          (string-join (data-space)
                                                       (list-map (pipe pair-left
                                                                       pattern-to-sexp
                                                                       list-from
                                                                       stringify-sexps)
                                                                 cases))
                                          (string-of-char 10)
                                          (data-error-stack-trace)
                                          (string-of-char 10)
                                          (render-stack-trace stack-trace))))))

(def eval-int32-operator (operator scope stack-trace arguments)
     (match arguments
            (Cons (ValueInteger a) (Cons (ValueInteger b) Empty))
                (return (ValueInteger (operator a b)))
            _
                (Error (data-error-int32-op))))

(def eval-int32-less-than (scope stack-trace arguments)
     (match arguments
            (Cons (ValueInteger a) (Cons (ValueInteger b) (Cons first (Cons second Empty))))
                (match (< a b)
                       True   (return first)
                       False  (return second))
            _
                (Error (data-error-int32-op))))

(def eval-function-application (eval-expression stack-trace arguments values scope expression)
     (match (< (list-size values) (list-size arguments))
            True
                (return (ValueClosure (list-skip (list-size values) arguments)
                                      expression
                                      (scope-add (list-zip (list-map symbol-token arguments) values) scope)))
            False
                (eval-expression (scope-add (list-zip (list-map symbol-token arguments) values) scope)
                                 stack-trace
                                 expression)))

(def apply-function (eval-expression scope stack-trace expression values)
    (match values
           (Cons (ValueClosure arguments body closed-scope) parameters)
               (eval-function-application eval-expression
                                          (add-stack-trace expression parameters stack-trace)
                                          arguments
                                          parameters
                                          closed-scope
                                          body)
           (Cons (ValueFunction arguments body) parameters)
               (eval-function-application eval-expression
                                          (add-stack-trace expression parameters stack-trace)
                                          arguments
                                          parameters
                                          scope
                                          body)
           (Cons (ValueIntrinsic evaluator) parameters)
               (evaluator scope stack-trace (list-rest values))
           _
               (eval-error)))

(def eval-expression (scope stack-trace expression)
     (match expression
            (IntegerConstant value _)
                (return (ValueInteger value))
            (Variable sym)
                (match (scope-get (symbol-token sym) scope)
                       (Some value)
                            (return value)
                       None
                            (Error (string-join (string-empty)
                                                (list (data-error-no-symbol)
                                                      (data-space)
                                                      (symbol-name sym)
                                                      (string-of-char 10)
                                                      (data-error-stack-trace)
                                                      (string-of-char 10)
                                                      (render-stack-trace stack-trace)))))
            (Constructor sym arguments _)
                (bind (sequence (list-map (eval-expression scope stack-trace) arguments)) (fn (values)
                (return (ValueConstructor (symbol-token sym) (symbol-name sym) values))))
            (Lambda arguments expression _)
                (return (ValueClosure arguments expression scope))
            (FunctionApplication expressions _)
                (bind (sequence (list-map (eval-expression scope stack-trace) expressions))
                (apply-function eval-expression scope stack-trace (list-first expressions)))
            (Match expression cases _)
                (bind (eval-expression scope stack-trace expression) (fn (expression)
                (pattern-match eval-expression scope stack-trace expression cases)))))

(def eval-pipe' (functions scope stack-trace parameters)
     (match functions
            Empty
                (match (list-first parameters)
                       None          (eval-error)
                       (Some value)  (return value))
            (Cons function rest)
                (bind (apply-function eval-expression scope stack-trace None (Cons function parameters)) (fn (value)
                (eval-pipe' rest scope stack-trace (list value))))))

(def eval-pipe (scope stack-trace functions)
     (return (ValueIntrinsic (eval-pipe' functions))))

(def eval-list' (values)
     (match values
            Empty
                (ValueConstructor (sym-Empty) (data-Empty) Empty)
            (Cons value rest)
                (ValueConstructor (sym-Cons) (data-Cons) (list value (eval-list' rest)))))

(def eval-list (scope stack-trace values)
     (return (eval-list' values)))

(def eval (scope values)
     (match (scope-get (sym-main) scope)
            (Some (ValueFunction arguments expression))
                (eval-expression (scope-add (list-zip (list-map symbol-token arguments) values) scope)
                                 Empty
                                 expression)
            _
                (eval-error)))

(def add-intrinsics (scope)
     (scope-add (list (Pair (sym-+)                (ValueIntrinsic (eval-int32-operator +)))
                      (Pair (sym--)                (ValueIntrinsic (eval-int32-operator -)))
                      (Pair (sym-*)                (ValueIntrinsic (eval-int32-operator *)))
                      (Pair (sym-/)                (ValueIntrinsic (eval-int32-operator /)))
                      (Pair (sym-%)                (ValueIntrinsic (eval-int32-operator %)))
                      (Pair (sym-&)                (ValueIntrinsic (eval-int32-operator &)))
                      (Pair (sym-int32-less-than)  (ValueIntrinsic eval-int32-less-than))
                      (Pair (sym-pipe)             (ValueIntrinsic eval-pipe))
                      (Pair (sym-list)             (ValueIntrinsic eval-list)))
                scope))

(def definitions-to-scope (definitions)
     (scope-add-definitions definitions (add-intrinsics (scope-empty))))

(def main-takes-argument (scope)
     (match (scope-get (sym-main) scope)
            (Some (ValueFunction (Cons _ __) ___))
                True
            _
                False))

(def box-maybe (maybe)
     (match maybe
            (Some x)  (ValueConstructor -2000 (data-Some) (list (ValueInteger x)))
            None      (ValueConstructor -2001 (data-None) Empty)))

(def box-get (collection get arguments)
     (match arguments
            (Cons _ (Cons (ValueInteger i) Empty))
                (return (box-maybe (get collection i)))
            _
                (eval-error)))

(def box-next' (value iterator)
     (return (ValueConstructor -2002 (data-Pair)
                               (list (box-maybe value)
                                     (ValueNativeIterator iterator)))))

(def box-next (scope stack-trace arguments)
     (match arguments
            (Cons (ValueNativeIterator iterator) Empty)
                (match (indexed-iterator-next iterator)
                       (Pair value next-iterator)
                            (box-next' value next-iterator))
            _
                (eval-error)))

(def box-iterable-class (iterator)
     (ValueConstructor -2003 (data-IterableClass)
                       (list (ValueIntrinsic box-next))))

(def box-indexed-iterator (iterator)
     (ValueConstructor -2004 (data-IndexedIterator)
                       (list (box-iterable-class)
                             (ValueNativeIterator iterator)
                             (ValueInteger (indexed-iterator-index iterator)))))

(def add-identifiers (identifiers offset dictionary)
     (match identifiers
            (Cons identifier rest)
                (add-identifiers rest (- offset 1) (dictionary-set (identifier) (Pair offset (identifier)) dictionary))
            Empty
                dictionary))

(def internal-symbols ()
     (add-identifiers (list data-Some data-None data-Pair data-IterableClass data-IndexedIterator
                            data-main data-Empty data-Cons) -2000
     (with-local-transform-keywords (default-identifiers))))
