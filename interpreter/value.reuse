
(typ (value expression scope iterable)
     (ValueInteger int32)
     (ValueConstructor identifier (list (value expression scope iterable)))
     (ValueClosure (list identifier) expression scope)
     (ValueFunction identifier (list identifier) expression)
     (ValueIntrinsic (fn (scope (list string) (list (value expression scope iterable))) (result (value expression scope iterable) string)))
     (ValueSlice iterable))

(def value-function-to-string (arguments expression)
     (pipe
         expression
         expression-to-sexp
         list-from
         stringify-sexps
         (string-concat (data-space))
         (string-concat (wrap-in-brackets (string-join (data-space) (list-map identifier-name arguments))))
         (string-concat (data-space))
         (string-concat (data-fn))
         wrap-in-brackets))

(def value-to-string (value)
     (match value
            (ValueInteger integer)
                (string-from-int32 integer)
            (ValueConstructor identifier values)
                (match values
                       Empty
                           (identifier-name identifier)
                       _
                           (pipe
                               values
                               (list-map value-to-string)
                               (list-cons (identifier-name identifier))
                               (string-join (data-space))
                               wrap-in-brackets))
            (ValueClosure arguments expression scope)
                (value-function-to-string arguments expression)
            (ValueFunction _ arguments expression)
                (value-function-to-string arguments expression)
            (ValueSlice _)
                (data-slice)
            (ValueIntrinsic _)
                (data-intrinsic)))

(def value-constructor-identifier (value)
     (match value
            (ValueConstructor identifier _)
                (Some identifier)
            _
                None))

(def value-function-identifier (value)
     (match value
            (ValueFunction identifier _ __)
                (Some identifier)
            _
                None))
