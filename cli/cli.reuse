
(def usage (modules)
    (string-join (string-of-char 10) (list
          (data-usage1)
          (string-empty)
          (data-usage2)
          (string-empty)
          (data-usage3)
          (data-usage4)
          (data-usage5)
          (data-usage6)
          (data-usage7)
          (string-empty)
          (string-concat (data-usage8) (string-join (string-of-char 32) (list-map compiler-backend-name modules))))))

(typ cli-program (CliTime (fn (int32) cli-program))
                 (CliMaxHeapSize (fn (int32) cli-program))
                 (CliRenderSource (pair (list (pair identifier-transformation identifier)) source-string)
                                  (list (fn () string))
                                  (fn (slice) cli-program))
                 (CliReadFiles (list (pair module-reference string)) (fn ((list source-file)) cli-program))
                 (CliWriteFiles (list (pair string slice)) (fn () cli-program))
                 (CliOutput string (fn () cli-program))
                 (CliError string (fn () cli-program))
                 (CliExit int32))

(def flag-is-true? (flag default arguments)
     (match (dictionary-get flag arguments)
            (Some value)  (string-equal? value (data-true))
            None          default))

(def find-backend (arguments)
     (maybe-or-else (compiler-backend-ocaml)
     (maybe-bind (dictionary-get (data-language-flag) arguments)  (fn (language)
     (list-find-first (. (string-equal? language) compiler-backend-name) (compiler-backends))))))

(def modules-from-arguments (backend data-path arguments)
     ((pipe (list-filter (. (string-equal? (data-module-flag)) pair-left))
            (list-map (. (module-name-and-path False) pair-right))
            (match (flag-is-true? (data-parser-flag) False (dictionary-of arguments))
                   True   (list-cons (parser-module backend data-path))
                   False  id)
            (match (flag-is-true? (data-stdlib) True (dictionary-of arguments))
                   True   (list-concat (standard-library-module backend data-path))
                   False  id))
          arguments))

(def table-to-string (table)
     (string-join (string-of-char 10) (list-map (string-join (string-of-char 32)) table)))

(def print-diagnostics (arguments file-entries
                        max-heap-size
                        start-parse end-parse
                        start-transform end-transform
                        start-generate end-generate
                        start-read-files end-read-files
                        start-write-files end-write-files
                        k)
     (match (flag-is-true? (data-diagnostics-flag) False arguments)
            True
               (CliError (table-to-string (list 
                    (list (string-from-int32 (list-foldl (fn (file bytes) (+ bytes (source-file-size file))) 0 file-entries)) (data-bytes-read))
                    (list (string-from-int32 max-heap-size) (data-max-heap-size))
                    (list (string-from-int32 (- end-parse start-parse)) (data-parse-time))
                    (list (string-from-int32 (- end-transform start-transform)) (data-transform-time))
                    (list (string-from-int32 (- end-generate start-generate)) (data-generate-time))
                    (list (string-from-int32 (- end-read-files start-read-files)) (data-read-files))
                    (list (string-from-int32 (- end-write-files start-write-files)) (data-write-files))))
                                 k)
            False
               (k)))

(def format-input-files (input-files)
     (CliReadFiles (list-map (pair-cons ModuleSelf) input-files) (fn (source-files)
     (match (format-source-files source-files)
            (Result files-to-write)
                  (CliWriteFiles files-to-write (fn ()
                  (CliExit 0)))
            (Error error-message)
                  (CliError error-message (fn ()
                  (CliExit 1)))))))

(def test-case (test-number expression context)
     (string-join (string-empty) (list
        context
        (data-test-case-start)
        (string-from-int32 test-number)
        (data-test-case-middle)
        expression
        (data-test-case-end)
     )))

(def test-case-from-requirement (test-number requirement)
     (match requirement
        (ExpectSuccess context expression expected)
            (test-case test-number expression context)
        _
            (string-empty)))

(def test-cases-from-requirements (requirements)
     (list-foldr (. string-concat (pair-map test-case-from-requirement)) (string-empty) requirements))

(def compile-requirements (backend requirements)
     ((pipe test-cases-from-requirements
            (. source-file-generated string-to-slice)
            list-from
            (parse-source-files (with-local-transform-keywords (intrinsic-identifiers)))
            (compiler-backend-transform-definitions backend)
            (result-map (. pair-right (compiler-backend-generate-source backend (string-empty)))))
       requirements))

(def list-with-indexes (lst)
     (list-zip (list-from-range 0 (list-size lst)) lst))

(def parse-specification-files (file-entries)
     ((pipe (list-filter (. (source-file-type-equal? SourceFileTypeSpecification) source-file-type))
            (list-flatmap (. parse-spec source-file-content))
            list-with-indexes)
       file-entries))

(def test-preamble (file-entries)
     ((pipe (list-filter (pipe source-file-type (source-file-type-equal? SourceFileTypeSpecification) not))
            (list-map (. string-from-slice source-file-content))
            (string-join (string-of-char 10))
            source-string-string)
       file-entries))

(def generate-tests-from-input-files (input-files data-path argument-list)
     (match (dictionary-of argument-list)
            arguments
     (match (dictionary-get (data-output-key) arguments)
            None
                  (CliError (data-no-output-path) (fn ()
                  (CliExit 1)))
            (Some output-path)
     (match (find-backend arguments)  backend
     (CliReadFiles (list-concat (preamble-files backend data-path)
                                (list-map (pair-cons ModuleSelf) input-files)) (fn (file-entries)
     (match (parse-specification-files file-entries)
            numbered-requirements
     (match (compile-requirements backend numbered-requirements)
            (Result test-cases)
                   (match (compiler-backend-generate-tests backend numbered-requirements)
                            assertions
                                (CliRenderSource (Pair Empty (source-string-join (string-empty) (list (test-preamble file-entries) test-cases assertions))) (compiler-backend-reserved-identifiers backend) (fn (source)
                                (CliWriteFiles (list (Pair output-path source)) (fn ()
                                (CliExit 0))))))
            (Error error)
                  (CliError (error-to-string (list-empty) error) (fn ()
                  (CliExit 1)))))))))))

(def compile-input-files (input-files data-path argument-list)
     (match (dictionary-of argument-list)
            arguments
     (match (find-backend arguments)  backend
     (CliTime (fn (start-read-files)
     (CliReadFiles (list-concat (preamble-files backend data-path)
                   (list-concat (modules-from-arguments backend data-path argument-list)
                                (list-map (pair-cons ModuleSelf) input-files)))
                          (fn (file-entries)
     (CliTime (fn (end-read-files)
          (match (dictionary-get (data-output-key) arguments)
               (Some output-path)
                    (match (path-filename-without-extension output-path)
                           module-name
                    (CliTime (fn (start-parse)
                    (match (parse-source-files (with-local-transform-keywords (intrinsic-identifiers)) file-entries)
                           definitions
                    (CliTime (fn (end-parse)
                    (CliTime (fn (start-transform)
                    (match (compiler-backend-transform-definitions backend definitions)
                           definitions
                    (CliTime (fn (end-transform)
                    (CliTime (fn (start-generate)
                    (match (generate backend module-name definitions)
                           (Result source)
                              (CliTime (fn (end-generate)
                              (CliTime (fn (start-write-files)
                              (CliRenderSource source (compiler-backend-reserved-identifiers backend) (fn (source)
                              (CliWriteFiles (list (Pair output-path source)) (fn ()
                              (CliTime (fn (end-write-files)
                              (CliMaxHeapSize (fn (max-heap-size)
                              (print-diagnostics arguments file-entries
                                                 max-heap-size
                                                 start-parse end-parse
                                                 start-transform end-transform
                                                 start-generate end-generate
                                                 start-read-files end-read-files
                                                 start-write-files end-write-files (fn ()
                              (CliExit 0)))))))))))))))
                           (Error error)
                              (CliError (error-to-string file-entries error) (fn ()
                              (CliExit 1)))))))))))))))))
               None
                    (CliError (data-no-output-path) (fn ()
                    (CliExit 1)))))))))))))

(def cli-main (data-path argv)
     (match (parse-arguments argv)
          (CliArguments Empty Empty)
               (CliError (usage (compiler-backends)) (fn ()
               (CliExit 1)))
          (CliErrorMissingValue key)
               (CliError key (fn ()
               (CliExit 1)))
          (CliArguments argument-list input-files)
     (match (dictionary-of argument-list)
            arguments
     (match (dictionary-has? (data-h) arguments)
            True
                  (CliError (usage (compiler-backends)) (fn ()
                  (CliExit 1)))
            False
     (match (list-is-empty? input-files)
            True
                  (CliError (data-no-input-files) (fn ()
                  (CliExit 1)))
            False
     (match (dictionary-has? (data-f) arguments)
            True
                  (format-input-files input-files)
            False
     (match (dictionary-has? (data-internal-generate-tests) arguments)
            True
                  (generate-tests-from-input-files input-files data-path argument-list)
            False
                  (compile-input-files input-files data-path argument-list))))))))
